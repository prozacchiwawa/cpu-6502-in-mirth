
import base/maybe
import base/panic
export base/result
  type Result(e,t)
  Result.generate : e t Bool -- Result(e,t)

  err : e -- Result(e,t)
  ok  : t -- Result(e,t)

  err? : Result(e,t) -- Result(e,t) Bool
  ok?  : Result(e,t) -- Result(e,t) Bool

  result(f : *a e -- *b, g : *a t -- *b) : *a Result(e,t) -- *b
  rmap(f : a -- b) : Result(e,a) -- Result(e,b)
  rfor(f : *a t -- *a) : *a Result(e,t) -- *a
  rthen(f : a -- Result(e,b)) : Result(e,a) -- Result(e,b)


  result->maybe : Result(e,t) -- Maybe(t)

  # panics on err
  runwrap : Result(e,a) -- a

  # panics on ok
  runwrap_error : Result(e,a) -- e

end

data Result(e,t)
  err : e -- Result(e,t)
  ok  : t -- Result(e,t)
end

Result.generate = if(drop err, nip ok)

result(f,g) = match(err -> f, ok -> g)
rmap(f) = result(err, f ok)
rfor(f) = result(drop, f)
rthen(f) = result(err, f)

result->maybe = result(drop none, some)

err? = result(err true, ok false)
ok?  = result(err false, ok true)

err? drop == id
ok?  drop == id
err? == ok? not

runwrap = result("runwrap on err" panic, id)
runwrap_error = result(id, "runwrap_error on ok" panic)


